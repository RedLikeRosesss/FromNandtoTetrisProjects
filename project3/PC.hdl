// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    // inc
    Inc16(in=regOut, out=innerIncOut);
    Mux16(a=regOut, b=innerIncOut, sel=inc, out=incOut);

    // load
    Mux16(a=incOut, b=in, sel=load, out=loadOut);

    // reset
    Mux16(a=loadOut, b=false, sel=reset, out=resetOut);

    // else
    Or(a=inc, b=load, out=orOut1);
    Or(a=orOut1, b=reset, out=orOut2);

    Register(in=resetOut, load=orOut2, out=regOut);

    Or16(a=false, b=regOut, out=out);
}
